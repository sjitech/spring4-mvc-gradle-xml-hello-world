apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'eclipse-wtp'

// JDK 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

group   = 'jp.sji.bldemo'
version = '0.1'

configurations {
    // exclude log4j version 1
    all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
    all*.exclude group: "log4j", module: "log4j"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    // log4j2 config
    compile 'org.slf4j:log4j-over-slf4j:1.7.12'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.3'
    compile 'org.apache.logging.log4j:log4j-core:2.3'
    compile 'org.apache.logging.log4j:log4j-api:2.3'
    runtime 'org.apache.logging.log4j:log4j-web:2.3'

    compile 'org.springframework:spring-webmvc:4.2.0.RELEASE'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    runtime 'javax.servlet:jstl:1.2'

}

eclipse {
  wtp {
    component {
      contextPath = 'bldemo'
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

def user_home = System.getenv('HOME')

def config_tokens = [
    user_home_dir: user_home
]

task expand_web_xml_vars() {
    description 'Expand variables in web.xml'

    copy {
        from('src/main/webapp/WEB-INF') {
            include 'web.xml'
            expand(config_tokens)
        }
        into("$buildDir/exploded/WEB-INF")
    }
}

task exploded_clean(dependsOn: clean) {
    description "Just wrapper built-in clean task for empty $buildDir"
}

task exploded_create(type: Copy) {
    description 'Generate exploded war directory for web app'

    // expand vars in web.xml
    filesMatching("**/web.xml") {
        expand(config_tokens)
    }

    into "$buildDir/exploded"
    with war
}

war.dependsOn exploded_create

war {
    baseName = 'bldemo'
    version  = '0.1-SNAPSHOT'

    // expand vars in web.xml
    from 'src/main/webapp'
    exclude('WEB-INF/web.xml')
    webInf {
        from 'src/main/webapp/WEB-INF/web.xml'
        expand(config_tokens)
    }
    webXml = null
}

task exploded_update(dependsOn: expand_web_xml_vars) {
    description "Copy jsp and static resource to $buildDir/exploded"

    copy {
        from('src/main/resources') {
            include 'log4j2.xml'
        }
        into("$buildDir/exploded/WEB-INF/classes")
    }

    copy {
        from('src/main/webapp') {
            include '**/*.html'
            include '**/*.jsp'
            include '**/*.css'
            include '**/*.js'
            include '**/*.properties'
            include '**/*.png'
            include '**/*.jpeg'
            include '**/*.jpg'
            include '**/*.gif'
        }
        into "$buildDir/exploded"
    }
}


